#!/home/ryuichi/bin/swipl -q -t prolog,halt -s
% vi: ft=prolog
% OPTIONS:
%   -r <rev>    revision
:- use_module(library(optparse)).
:- use_module(library(process)).
:- use_module(library(error)).

optSpec([[
    opt(revision),
    type(integer),
    shortflags([r]),
    longflags(['revision']),
    help(['revision to which you want your vim upgraded'])
], [
    opt(quiet),
    type(boolean),
    shortflags([q])
], [
    opt(toplevelGoal),
    type(atom),
    shortflags([t])
], [
    opt(script),
    type(atom),
    shortflags([s])
]]).

prolog :-
    catch(body, E,
        ( print_message(error, E),
          fail
        )).

body :-
    getArg(revision(X)),
    goToVimDir,
    updateLocalRepository(X),
    removeConfigCache,
    configure,
    makeInstall,
    nl.

getArg(Term) :-
    current_prolog_flag(argv, Argv),
    optSpec(OptsSpec),
    opt_parse(OptsSpec, Argv, ParsedOpts, _),
    member(Term, ParsedOpts).

goToVimDir :-
    getenv('HOME', HOME),
    atomic_list_concat([HOME, repos, vim73], /, Path),
    working_directory(_, Path).

updateLocalRepository(X) :-
    must_be(positive_integer, X),
    process_create(path(hg), ['update', '-r', X], []).

removeConfigCache :-
    process_create(path(rm), ['src/auto/config.cache'], []).

configure :-
    machineType(MT),
    findall(Config, buildConfig(MT, Config), Configs),
    process_create(configure, Configs, []).

machineType(M) :-
    current_prolog_flag(arch, Arch),
    ( sub_atom(Arch, _, _, _, 'darwin') -> M = mac ; M = unix ), !.

makeInstall :-
    process_create(path(make), [], []),
    process_create(path(sudo), ['make', 'install'], []).

buildConfig(_,    '--with-features=normal').
buildConfig(_,    '--enable-luainterp').
buildConfig(_,    '--enable-rubyinterp').
buildConfig(_,    '--enable-multibyte').

buildConfig(unix, '--enable-gui=gtk2').
buildConfig(unix, '--with-x').

buildConfig(mac,  '--disable-gui').
buildConfig(mac,  Prefix) :-
    process_create(path(brew), ['--prefix'], [stdout(pipe(Out))]),
    read_line_to_codes(Out, Codes),
    atom_chars(PrefixAtom, Codes),
    atomic_concat('--prefix=', PrefixAtom, Prefix).
